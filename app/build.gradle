apply plugin: 'net.saliman.cobertura'

mainClassName = 'com.github.ksoichiro.groovy.example.Example'
eclipse.project.name = 'groovy-example-app'
cobertura {
    coverageFormats = ['xml', 'html']
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'

archivesBaseName = 'app'

run {
    classpath "${project.buildDir}/resources/main"
}

jar {
    excludes = ['**/*.groovy']
    manifest {
        attributes "Main-Class" : mainClassName
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

def copyFiles(String env) {
    copy {
        from "${project.buildDir}/libs"
        into "${project.buildDir}/copy/tmp/${env}/app/libs"
        include '**/*.jar'
    }
    copy {
        from 'src/main/resources'
        into "${project.buildDir}/copy/tmp/${env}/app/conf"
        include '**/*.groovy'
    }
    copy {
        from "src/${env}/resources"
        into "${project.buildDir}/copy/tmp/${env}/app/conf"
        include '**/*.groovy'
    }
}

Collection<String> environments = [
        'staging',
        'production'
]

environments.each { String env ->
    task("zip${env.capitalize()}", type: Zip) {
        appendix = env
        from "${project.buildDir}/copy/tmp/${env}"
    }

    task("emulate${env.capitalize()}", dependsOn: "pack${env.capitalize()}") << {
        copy {
            from zipTree("${buildDir}/distributions/${archivesBaseName}-${env}.zip")
            into "${buildDir}/emulation/${env}"
        }
        String cwd = "${buildDir}/emulation/${env}/${archivesBaseName}"
        javaexec {
            main mainClassName as String
            classpath file("${cwd}/libs/${archivesBaseName}.jar"), file("${cwd}/conf")
            workingDir cwd
        }
    }

    task("pack${env.capitalize()}", dependsOn: jar) << {
        copyFiles(env)
        tasks."zip${env.capitalize()}".execute()
    }
}
